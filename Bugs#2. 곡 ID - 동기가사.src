#
# * Mp3tag web source for Bugs / Fetch a synced lyric from found track ID
# v0.4.12 / 230830
# 
# * Installation
# - just copy this script to:
# %appdata%\Mp3tag\data\sources\
# - NOTE '#' in filename makes submenu!
#
[Name]=Bugs / 곡 ID - 동기가사
[BasedOn]=https://music.bugs.co.kr
[AlbumUrl]=https://music.bugs.co.kr/player/lyrics/T/
[WordSeparator]=+
[SearchBy]=Bugs 곡 ID||%BugsTrackId%||%s
[Encoding]=utf-8

# UserAgent
# ref: https://community.mp3tag.de/t/cant-get-full-browser-html-source-for-imdb-titles/41140/4
# default: "Mozilla/5.0 (compatible)", if enabled below: "Mp3tag/<version>"
# [UserAgent]=1

# ### Lyric API (from web player) URL example ###
# "Dear You" by Lovelyz
# LyricUrl (JSON): https://music.bugs.co.kr/player/lyrics/T/30233396

[ParserScriptAlbum]=...
# debug "off" "C:\Temp\Bugs\debug_lyrics.htm"
# DebugWriteInput "C:\Temp\Bugs\debug-input.out"
# ###################################################################
# This script parses the albums page for all common information.
#
# The current output variable is set via the outputto command
# All these fields are used by the confirm online information dialog
# ###################################################################
json "ON"

# "LYRICS" tag might contain LRC formatted lyrics
# ref: https://en.wikipedia.org/wiki/LRC_(file_format)

# created by
outputto "LYRICS"
json_select "userId"
Say "[by:"
SayRest
Say "]"
SayNewline

# actual lyrics
outputto "LYRICS"
json_select "lyrics"

# NOTE original lyrics format
# e.g. "23.3|네가 좋아 쉽게 말해도＃27.2|지금 내 맘조차 가볍단 건 아니야＃..." (31333108: 찾아가세요 / 러블리즈)
# e.g.2. "014|또 하루 멀어져간다＃022|내뿜은 담배 연기처럼＃030|작기만한 내 기억속에..." (38725: 서른 즈음에 / 김광석)
# e.g.3. "2|웃지 못할사건 또 잊지못할...(omitted)... 서리＃12|아 근데 남자들도 만만치가 않아＃15|어느날 우연히..."

# cleanup for conversion to LRC format
RegexpReplace "(\.\d)\|" "\10|"        # "ss.x" --> "ss.x0"

## for oldies (see e.g.2, e.g.3)
RegexpReplace "0(\d{2})\|" "\1|"       # "0ss|" --> "ss|"
RegexpReplace "(\d)\|" "\1.00|"        # "s|" --> "s.00|"

# preserve 'ms' numbers from `SayDuration`, which ignores numbers under a decimal point ... [1]
RegexpReplace "\.(\d{2})\|" "_\1|"     # ".ms|" --> "_ms|"

# closing bracket of timestamp
RegexpReplace "\|" "]"                 # bar to closing bracket
# NOTE opening bracket is printed in the loop

# TODO place an meaningless opening bracket, which solves(?) omitted first (10th second) time number
RegexpReplace "^" "["


Do
  SkipChars "＃"

  Say "["

  SayDuration "s" 0 0

  # HARDCODED return '_' to '.' ... [1]
  MoveChar 1
  Say "."

  ## dump remaining line to tag
  SayUntil "＃"
  SayNewline
While "＃"
