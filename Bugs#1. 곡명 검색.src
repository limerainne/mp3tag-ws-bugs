#
# * Mp3tag web source for Bugs / Search by Song Title
# v0.4.8 / 200211
# 
# * Installation
# - just copy this script to:
# %appdata%\Mp3tag\data\sources\
# - NOTE '#' in filename makes submenu!
# 
# ---- Original by...
# Mp3tag parsing for Melon.com
#
# Created by Hyosang Park http://hyosang.kr
# Last modified 2016.11.02
#
# Save it in your Mp3tag data directory, i.e.
# C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
# ----
#
[Name]=Bugs / 곡명 검색 (앨범 정보 이용)
[Include]=Bugs#Common_AlbumPage.inc

[IndexUrl]=https://m.bugs.co.kr/api/getSearchList?type=track&query=
[WordSeparator]=+
[IndexFormat]=%_url%|%_preview%|%곡명%|%아티스트%|%앨범%|%발매일%
[SearchBy]=곡명||$regexp(%title%,'[+!{}\[\]^"~*?:\\`]',)||%s&page=1&size=50
[Encoding]=url-utf-8

# ### Album URL example ###
# "A New Trilogy" by Lovelyz
# AlbumUrl: https://m.bugs.co.kr/album/20032445
# CoverUrl: https://image.bugsm.co.kr/album/images/<size>/200324/20032445.jpg
# -- size: 200, 500, 1000, original
# -- note: care for last 2 digit! (200324 -> 200324'45')

[ParserScriptIndex]=...
debug "off" "C:\Temp\Bugs\debug_search.htm"
# ###################################################################
# This script parses the webpage for all releases
# by this albumname.
#
# The output format is defined by IndexFormat above, which
# is used by the selection dialog
# ###################################################################
# * From Feb, 2020, Bugs mobile website was revised to use AJAX search API!
json "ON"  # turn on JSON parser

json_foreach "list"

  json_select_object "album"

      json_select "album_id"
      sayrest
      say "|"

      json_select "album_id"
      say "https://music.bugs.co.kr/album/"
      sayrest
      say "|"

  json_unselect_object

  json_select "track_title"
  sayrest
  say "|"

  json_select_many "artists" "artist_nm" ", "
  sayrest
  say "|"

  json_select_object "album"
  
    json_select "title"
    sayrest
    say "|"

    json_select "release_ymd"
    RegexpReplace "(\d{4})(\d{2})(\d{2})" "$1.$2.$3"
    sayrest

  json_unselect_object

  saynewline

json_foreach_end
# end of json_foreach "list"  # NOTE DO NOT PLACE COMMENTS WITH THE SAME LINE OF THE COMMAND
